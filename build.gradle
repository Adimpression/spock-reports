buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "com.github.townsfolk:gradle-release:1.2"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.1"
    id 'groovy'
    id 'idea'
    id 'maven-publish'
}

defaultTasks 'clean', 'jar', 'test'

group = 'com.athaydes'
version = "1.2.8"

def groovyVersion = '2.0.8'
def spockVersion = '1.0-groovy-2.0'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "org.spockframework:spock-core:${spockVersion}"
    compile 'org.slf4j:slf4j-api:1.7.13'

    testCompile "cglib:cglib-nodep:2.2.2"
}

test {
    maxParallelForks = 4
	exclude '**/FakeTest.class'
	systemProperty 'project.buildDir', project.buildDir
	testLogging.showStandardStreams = true
	beforeTest { descriptor ->
		logger.lifecycle( "Running test: " + descriptor )
	}
}

jar {
	manifest {
		attributes(
				"Implementation-Title": "Athaydes-Spock-Reports",
				"Implementation-Version": version )
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

apply plugin: 'release'

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    pkg {
        repo = 'maven'
        name = 'spock-reports'
        licenses = ['Apache-2.0']
    }
}
bintrayUpload.dependsOn build, sourcesJar
createReleaseTag.dependsOn bintrayUpload

